plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.116'
    id 'idea'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter { includeGroup "maven.modrinth" }
    }

    exclusiveContent {
        forRepository { maven { url "https://echoellet.github.io/Controlify/" } }
        filter { includeGroup "dev.echoellet" }
    }
}

base {
    archivesName = mod_id
    version = "${mod_version}-mc${minecraft_version}-forge"
}

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"
    validateAccessTransformers = true

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.minecraft_version
    }

    runs {
        client {
            client()

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    modImplementation("dev.echoellet:controlify-forgified:2.1.3-mc1.20.1-forge")
    modImplementation "maven.modrinth:epic-fight:20.13.4-1.20.1"
    modImplementation "maven.modrinth:epic-fight-skill-tree:20.2.2-1.20.1"
    modRuntimeOnly "maven.modrinth:in-game-account-switcher:2ByghZuX"
    modRuntimeOnly("maven.modrinth:yacl:3.6.6+1.20.1-forge")
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
legacyForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

publishMods {
    dryRun = false
    file = reobfJar.archiveFile
    additionalFiles.from(tasks.named("sourcesJar").flatMap { it.archiveFile })
    changelog = project.file('RELEASE_NOTES.md').getText()
    type = STABLE
    modLoaders.add("forge")
    displayName = "[Forge 1.20.1] v${mod_version}"

    curseforge {
        projectId = "1372116"
        projectSlug = "epic-fight-controlify"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add("1.20.1")
        javaVersions.add(JavaVersion.VERSION_17)
        clientRequired = true
        serverRequired = false

        requires("controlify-forgified-unofficial")
    }
    modrinth {
        projectId = "56eSX8Rj"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add("1.20.1")

        requires("controlify-forgified")
        projectDescription = providers.fileContents(layout.projectDirectory.file("README.md")).asText
    }
    github {
        repository = "EchoEllet/epicfight-controlify"
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        commitish = "main"
    }
}
